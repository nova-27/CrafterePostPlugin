plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.palantir.git-version' version '0.15.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.2'
}

group 'com.github.nova_27.mcplugin'
version versionDetails().lastTag.substring(1)

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.9'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    implementation 'com.github.Querz:NBT:6.1'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
}

shadowJar {
    if (project.hasProperty('outputDir'))
        getDestinationDirectory().set(new File(outputDir))

    relocate 'org.bstats', "${project.group}.${rootProject.name.toLowerCase()}"
}

bukkit {
    name = rootProject.name
    version = project.version
    main = "${project.group}.${rootProject.name.toLowerCase()}.${rootProject.name}"
    description = 'CrafterePost公式プラグイン'
    website = 'https://crafterepost.netlify.app/'
    apiVersion = '1.13'
    author = 'nova27'
    depend = ['WorldEdit', 'ProtocolLib']

    commands {
        crapos {
            description = 'Schematic建築ファイル / MCSR録画ファイル を出力します'
            usage = 'usage: /<command> <schem|record>'
            aliases = ['crafterepost']
        }
    }
}